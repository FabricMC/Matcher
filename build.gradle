plugins {
	id 'com.diffplug.spotless' apply false
	id 'org.gradlex.extra-java-module-info' apply false
	id 'com.gradleup.shadow' apply false
	id 'me.modmuss50.mod-publish-plugin' apply false
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'checkstyle'
	apply plugin: 'com.diffplug.spotless'
	apply plugin: 'com.gradleup.shadow'
	apply plugin: 'org.gradlex.extra-java-module-info'
	apply plugin: 'eclipse'
	apply plugin: 'me.modmuss50.mod-publish-plugin'

	repositories {
		mavenCentral()
		maven {
			name = "Fabric"
			url = 'https://maven.fabricmc.net/'
		}
	}

	group = 'net.fabricmc.matcher'
	version = rootProject.version

	def ENV = System.getenv()

	checkstyle {
		configFile = rootProject.file("checkstyle.xml")
		toolVersion = rootProject.checkstyle_version
	}

	spotless {
		lineEndings = com.diffplug.spotless.LineEnding.UNIX

		java {
			removeUnusedImports()
			importOrder('java.', 'javax.', '', 'net.fabricmc.', 'matcher.')
			indentWithTabs()
			trimTrailingWhitespace()
		}
	}

	extraJavaModuleInfo {
		failOnMissingModuleInfo.set(false) // because of transitive dependencies

		// JCommander
		automaticModule("com.beust:jcommander", "jcommander")

		// CFR
		automaticModule("net.fabricmc:cfr", "cfr")

		// Vineflower
		automaticModule("org.vineflower:vineflower", "org.vineflower.vineflower")

		// Procyon
		automaticModule("org.bitbucket.mstrobel:procyon-compilertools", "procyon.compilertools")

		// JADX
		automaticModule("io.github.skylot:jadx-core", "jadx.core")
		automaticModule("io.github.skylot:jadx-java-input", "jadx.plugins.java_input")
	}

	java {
		withJavadocJar()
		withSourcesJar()
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"
	}

	tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
		// https://github.com/johnrengelman/shadow/issues/882#issuecomment-1715703146
		// Defer the resolution  of 'runtimeClasspath'. This is an issue in the shadow
		// plugin that it automatically accesses the files in 'runtimeClasspath' while
		// Gradle is building the task graph. The three lines below work around that.
		inputs.files(project.configurations.runtimeClasspath)
		configurations = Collections.emptyList()

		doFirst {
			configurations = Collections.singletonList(project.configurations.runtimeClasspath)
		}
	}

	javadoc {
		options.addBooleanOption('html5', true)

		// disable 'missing' warnings
		options.addStringOption('Xdoclint:all,-missing', '-quiet')
	}

	publishing {
		publications {
			"$project.name"(MavenPublication) {
				from components.java
			}
		}

		repositories {
			if (ENV.MAVEN_URL) {
				repositories.maven {
					name = "fabric"
					url = ENV.MAVEN_URL
					credentials {
						username = ENV.MAVEN_USERNAME
						password = ENV.MAVEN_PASSWORD
					}
				}
			}
		}
	}

	// Publish the fat platform specific jars to GitHub releases
	publishMods {
		changelog = providers.environmentVariable("CHANGELOG").getOrElse("No changelog provided")
		type = STABLE
		dryRun = providers.environmentVariable("GITHUB_TOKEN").map { false }.getOrElse(true)

		file = jar.archiveFile

		platforms.forEach { platform ->
			additionalFiles.from tasks.named(platform + "Jar", ShadowJar).map { it.archiveFile }
		}

		github {
			accessToken = providers.environmentVariable("GITHUB_TOKEN")
			repository = providers.environmentVariable("GITHUB_REPOSITORY").getOrElse("FabricMC/dryrun")
			commitish = providers.environmentVariable("GITHUB_REF_NAME").getOrElse("dryrun")
		}
	}
}
