import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
	id 'matcher.common'
	id 'matcher.publishing.maven'
	id 'matcher.publishing.github.base'
	id 'org.openjfx.javafxplugin'
	id 'org.gradlex.extra-java-module-info'
	id 'application'
}

base {
	archivesName = 'matcher-gui'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

def platforms = [
	"win",
	"linux",
	"mac",
	"mac-aarch64"
]

def fxModules = [
	"javafx-base",
	"javafx-graphics",
	"javafx-controls",
	"javafx-web",
	"javafx-media"
]

javafx {
	version = javafx_version
	modules = fxModules.collect {
		module -> module.replace('-', '.')
	}
}

configurations {
	platforms.each { platform ->
		register(platform)
	}

	runtimeWithoutNatives {
		extendsFrom runtimeClasspath
		canBeConsumed = false
		canBeResolved = true
		exclude group: 'org.openjfx'
	}
}

dependencies {
	api project(':matcher-core')
	api "com.github.javaparser:javaparser-core:${javaparser_version}"
	api "net.fabricmc:cfr:${fabric_cfr_version}"
	api "org.bitbucket.mstrobel:procyon-compilertools:${procyon_version}"
	implementation "org.ow2.asm:asm-util:${asm_version}"
	implementation "org.vineflower:vineflower:${vineflower_version}"
	implementation ("io.github.skylot:jadx-core:${jadx_version}") {
		exclude group: 'com.android.tools.build', module: 'aapt2-proto'
	}
	implementation ("io.github.skylot:jadx-java-input:${jadx_version}") {
		exclude group: 'io.github.skylot', module: 'raung-disasm'
	}
	runtimeOnly "org.tinylog:tinylog-impl:${tinylog_version}"
	runtimeOnly "org.tinylog:slf4j-tinylog:${tinylog_version}"

	platforms.each {platform ->
		fxModules.each {module ->
			add(platform, "org.openjfx:${module}:${javafx_version}:${platform}") {
				transitive = false
			}
		}
	}
}

extraJavaModuleInfo {
	// CFR
	automaticModule("net.fabricmc:cfr", "cfr")

	// Vineflower
	automaticModule("org.vineflower:vineflower", "org.vineflower.vineflower")

	// Procyon
	automaticModule("org.bitbucket.mstrobel:procyon-compilertools", "procyon.compilertools")
	automaticModule("org.bitbucket.mstrobel:procyon-core", "procyon.core")

	// JADX
	automaticModule("io.github.skylot:jadx-core", "jadx.core")
	automaticModule("io.github.skylot:jadx-java-input", "jadx.plugins.java_input")
	automaticModule("io.github.skylot:jadx-input-api", "jadx.api.input")
}

application {
	mainModule = 'matcher.gui'
	mainClass = 'matcher.gui.Main'
}

jar {
	processResources.exclude('tinylog-dev.properties')
}

platforms.forEach { platform ->
	def platformJar = tasks.register(platform + "FatJar", ShadowJar) {
		archiveClassifier = platform + '-all'
		from sourceSets.named('main').map { it.output }
		configurations.add(project.configurations.runtimeWithoutNatives)
		configurations.add(project.configurations.getByName(platform))

		exclude 'META-INF/*.RSA'
		exclude 'META-INF/*.SF'

		manifest {
			attributes 'Main-Class': application.mainClass.get()
		}
	}

	tasks.named('assemble') {
		dependsOn platformJar
	}
}

publishMods {
	platforms.forEach { platform ->
		if (platform == "win") {
			file = winFatJar.archiveFile // publishMods needs one main file
		} else {
			additionalFiles.from tasks.named(platform + "FatJar", ShadowJar).map { it.archiveFile }
		}
	}
}
